/* First entry defines the entire thing parsed */
expression: multicompare;

/* These don't short-circuit, partly b/c the expression evaluator ALWAYS
parses all variables before evaluating anything */
multicompare: compare;
multicompare: compare "&&" compare {binop};
multicompare: compare "||" compare {binop};
/* Convenience aliases */
multicompare: compare "&" compare {binop};
multicompare: compare "|" compare {binop};

/* Note that "1 < 2 < 3" is not supported, to allow future expansion
with possible Python-style semantics */
compare: add;
compare: add "<" add {binop};
compare: add "<=" add {binop};
compare: add "==" add {binop};
compare: add "!=" add {binop};
compare: add ">=" add {binop};
compare: add ">" add {binop};
/* Convenience aliases */
compare: add "=" add {binop};
compare: add "=>" add {binop};

add: multiply;
add: add "+" multiply {binop};
add: add "-" multiply {binop};
multiply: expon;
multiply: multiply "*" expon {binop};
multiply: multiply "x" expon {binop}; /* Humans like to write 3x4 for multiplication */
multiply: multiply "/" expon {binop};
multiply: multiply "%" expon {binop};
expon: value;
expon: value "**" expon {binop};
expon: value "^" expon {binop};

digit: "0";
digit: "1";
digit: "2";
digit: "3";
digit: "4";
digit: "5";
digit: "6";
digit: "7";
digit: "8";
digit: "9";
integer: digit;
integer: digit integer {stitch};
float: integer "." {stitch};
float: "." integer {stitch};
float: integer "." integer {stitch};
signedint: integer;
signedint: "+" integer {stitch};
signedint: "-" integer {stitch};
signedfloat: float;
signedfloat: "+" float {stitch};
signedfloat: "-" float {stitch};
value: signedint {makeint};
value: signedfloat {makefloat};
value: "string";
value: "(" expression ")" {parens};
value: "word" "(" ")" {func_nullary};
value: "word" "(" arglist ")" {func};
value: "@" "word" {varlookup};
value: "@" "string" {varlookup};
/* This syntax is very weird - notably, "@var=1" will parse as "@var" (lookup) followed
by "=1" (comparison), and the correct syntax is "@=var=1". Also, the value must be
parenthesized if it is itself an expression; this avoids a bunch of shift/reduce conflicts.
This entire feature is undocumented and has no backward-compatibility guarantee; if I'm
ever going to make this an official feature, I'll want to make the syntax way better. */
value: "@" "=" "word" "=" value {varassign};
value: "@" "=" "string" "=" value {varassign};

arglist: expression {make_array};
arglist: expression "," {make_array};
arglist: expression "," arglist {prepend_array};
